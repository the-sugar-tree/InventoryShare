plugins {
    id ("org.cadixdev.licenser") version "0.6.1"
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'org.cadixdev.licenser'

    group = 'com.sugar_tree.inventoryshare'
    version = '3.1.1-beta'
    description = 'Minecraft Plugin'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    compileJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            name 'spigotmc-repo'
            url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        }
        maven {
            name 'papermc-repo'
            url 'https://repo.papermc.io/repository/maven-public/'
        }
        maven {
            name 'sponge-repo'
            url 'https://repo.spongepowered.org/repository/maven-public/'
        }
        maven {
            name 'dmulloy2(ProtocolLib)-repo'
            url 'https://repo.dmulloy2.net/repository/public/'
        }
        maven {
            url 'https://libraries.minecraft.net/'
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.26'
        annotationProcessor 'org.projectlombok:lombok:1.18.26'
        compileOnly 'org.jetbrains:annotations:23.0.0'
    }

    jar {
        from '../LICENSE.txt'
        doLast {
            copy {
                from("build/libs") {
                    include '*.jar'
                }
                into(rootProject.file("build/libs"))
            }
        }
    }

    license {
        header = rootProject.file('HEADER.txt')
        include '**/*.java'
        newLine = true
        properties {
            name = 'the-sugar-tree'
            year = '2023'
        }
    }

    jar.dependsOn('licenseFormat')
}

test.enabled(false)
jar.enabled(false)
compileJava.enabled(false)

dependencies {
    testCompileOnly 'org.projectlombok:lombok:1.18.26'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.26'
    testImplementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'

    testCompileOnly 'org.spigotmc:spigot-api:1.13-R0.1-SNAPSHOT'
//    testCompileOnly 'org.spigotmc:spigot-api:1.20.1-R0.1-SNAPSHOT'
}

jar {
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    duplicatesStrategy = 'EXCLUDE'
}

test {
    useJUnitPlatform()
}